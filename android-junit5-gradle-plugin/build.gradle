apply plugin: "groovy"
apply plugin: "kotlin"
apply plugin: "java-gradle-plugin"
apply plugin: "jacoco"

// ------------------------------------------------------------------------------------------------
// Compilation Tweaks
//
// The plugin currently consists of a codebase wherein Groovy & Kotlin coexist.
// Therefore, the compilation chain has to be well-defined to allow Kotlin
// to call into Groovy code.
//
// The other way around ("call Kotlin from Groovy") is prohibited explicitly.
// ------------------------------------------------------------------------------------------------
compileGroovy.dependsOn = compileGroovy.taskDependencies.values - "compileJava"
compileKotlin.dependsOn compileGroovy
compileKotlin.classpath += files(compileGroovy.destinationDir)
classes.dependsOn compileKotlin

// ------------------------------------------------------------------------------------------------
// Plugin Resource Setup
//
// This block generates required resource files
// that provide additional configuration to the plugin.
// This includes setting up the plugin identifiers
// with which the plugin can be applied to consumer projects,
// as well as the population of marker tokens inside
// the versions.properties resource file.
// ------------------------------------------------------------------------------------------------

final def pluginClassName =
    "de.mannodermaus.gradle.plugins.junit5.AndroidJUnitPlatformPlugin"

gradlePlugin {
  plugins {
    shortIdentifier {
      id = "android-junit5"
      implementationClass = pluginClassName
    }
    longIdentifier {
      id = "de.mannodermaus.android-junit5"
      implementationClass = pluginClassName
    }
  }
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
  def tokens = [ANDROID_JUNIT5_VERSION: PLUGIN_VERSION_NAME,
                JUNIT4_VERSION        : JUNIT4_VERSION,
                JUNIT_PLATFORM_VERSION: JUNIT_PLATFORM_VERSION,
                JUNIT_JUPITER_VERSION : JUNIT_JUPITER_VERSION,
                JUNIT_VINTAGE_VERSION : JUNIT_VINTAGE_VERSION]

  inputs.properties(tokens)

  from(sourceSets.main.resources.srcDirs) {
    include '**/versions.properties'
    filter(ReplaceTokens, tokens: tokens)
  }
}

// For some strange reason, running :android-junit5:test will
// get the Gradle daemon stuck with an unknown cause.
// Overwrite the test task entirely to mitigate this
// (the actual plugin tests have their own modules anyway).
task test(overwrite: true) {}

// ------------------------------------------------------------------------------------------------
// Dependency Definitions
// ------------------------------------------------------------------------------------------------

dependencies {
  // Language Support
  compile gradleApi()
  compile "org.codehaus.groovy:groovy:$GROOVY_VERSION"
  compile "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"

  // Dependent Plugins
  compile "org.junit.platform:junit-platform-gradle-plugin:$JUNIT_PLATFORM_VERSION"
  compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
  compileOnly "com.android.tools.build:gradle:$ANDROID_PLUGIN_3X_VERSION"
}

// ------------------------------------------------------------------------------------------------
// Deployment Setup
//
// Releases are pushed to jcenter via Bintray, while snapshots are pushed to Sonatype OSS.
// This section defines the necessary tasks to push new releases and snapshots using Gradle tasks.
// ------------------------------------------------------------------------------------------------
project.ext.deployConfig = [platform        : "java",
                            artifactId      : PLUGIN_ARTIFACT_ID,
                            version         : PLUGIN_VERSION_NAME,
                            license         : PLUGIN_LICENSE_NAME,
                            description     : PLUGIN_DESCRIPTION,
                            beforePublishDo : null,
                            mavenPublication: {
                              from components.java
                              artifact sourcesJar
                              artifact javadocJar
                              groupId PLUGIN_GROUP_ID
                              artifactId PLUGIN_ARTIFACT_ID
                              version PLUGIN_VERSION_NAME
                              pom.withXml {
                                def root = asNode()
                                root.appendNode("description", PLUGIN_DESCRIPTION)
                                root.appendNode("name", PLUGIN_ARTIFACT_ID)
                                root.appendNode("url", VCS_URL)
                              }
                            }]
apply from: "$rootDir/gradle/deployment.gradle"
