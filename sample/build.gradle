buildscript {
  repositories {
    google()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
  }

  dependencies {
    classpath "de.mannodermaus.gradle.plugins:android-junit5:$PLUGIN_VERSION_NAME_LATEST_STABLE"
  }
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: "jacoco"

android {
  compileSdkVersion COMPILE_SDK_VERSION

  dexOptions {
    javaMaxHeapSize JAVA_MAX_HEAP_SIZE
  }

  defaultConfig {
    applicationId "de.mannodermaus.junit5.sample"
    minSdkVersion SAMPLE_MIN_SDK_VERSION
    targetSdkVersion TARGET_SDK_VERSION
    versionCode 1
    versionName "1.0"

    // Make sure to use the AndroidJUnitRunner (or a sub-class) in order to hook in the JUnit 5 Test Builder
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    testInstrumentationRunnerArgument "runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder"
  }

  // Since the minSdkVersion requirement for JUnit 5 Instrumentation Tests is quite high,
  // we introduce a product flavor that uses an elevated version other than the application's default.
  // With this, we are able to try JUnit 5 tests without sacrificing the minSdkVersion completely.
  flavorDimensions "kind"
  productFlavors {
    experimental {
      dimension "kind"
      minSdkVersion 26
    }

    normal {
      dimension "kind"
    }
  }

  // Add Kotlin source directory to all source sets
  sourceSets.each {
    it.java.srcDirs += "src/$it.name/kotlin"
  }

  compileOptions {
    targetCompatibility JavaVersion.VERSION_1_8
    sourceCompatibility JavaVersion.VERSION_1_8
  }

  testOptions {
    junitPlatform {
      // Configure JUnit 5 tests here
    }
    unitTests.all {
      testLogging.events = ["passed", "skipped", "failed"]
    }
  }

  packagingOptions {
    exclude "META-INF/LICENSE.md"
    exclude "META-INF/LICENSE-notice.md"
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"

  testImplementation "org.junit.jupiter:junit-jupiter-api:$JUNIT_JUPITER_VERSION"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$JUNIT_JUPITER_VERSION"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$JUNIT_JUPITER_VERSION"

  androidTestImplementation "junit:junit:$JUNIT4_VERSION"
  androidTestImplementation "com.android.support.test:runner:$SUPPORT_TEST_LIBRARY_VERSION"

  // Add the Android Instrumentation Test dependencies to the product flavor only
  // (with this, only the "experimental" flavor must have minSdkVersion 26)
  androidTestExperimentalImplementation "org.junit.jupiter:junit-jupiter-api:$JUNIT_JUPITER_VERSION"
  androidTestExperimentalImplementation "de.mannodermaus.junit5:android-instrumentation-test:$INSTRUMENTATION_VERSION_NAME_LATEST_STABLE"

  // Runtime dependencies for Android Instrumentation Tests
  androidTestExperimentalRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$JUNIT_JUPITER_VERSION"
  androidTestExperimentalRuntimeOnly "org.junit.platform:junit-platform-runner:$JUNIT_PLATFORM_VERSION"
  androidTestExperimentalRuntimeOnly "de.mannodermaus.junit5:android-instrumentation-test-runner:$INSTRUMENTATION_VERSION_NAME_LATEST_STABLE"
}
