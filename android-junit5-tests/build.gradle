import de.mannodermaus.gradle.plugins.junit5.WriteClasspathResource
import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: "groovy"
apply plugin: "java-gradle-plugin"
apply plugin: "java-library"
apply plugin: "idea"
apply plugin: "jacoco"
apply plugin: "kotlin"

// ------------------------------------------------------------------------------------------------
// Compilation Tweaks
//
// The plugin currently consists of a codebase wherein Groovy & Kotlin coexist.
// Therefore, the compilation chain has to be well-defined to allow Kotlin
// to call into Groovy code.
//
// The other way around ("call Kotlin from Groovy") is prohibited explicitly.
// ------------------------------------------------------------------------------------------------
compileTestGroovy.dependsOn = compileTestGroovy.taskDependencies.mutableValues - "compileTestJava"
compileTestKotlin.dependsOn compileTestGroovy
compileTestKotlin.classpath += files(compileTestGroovy.destinationDir)
testClasses.dependsOn compileTestKotlin

// Add custom dependency configurations
configurations {
  functionalTest {
    description = "Local dependencies used for compiling & running " +
        "tests source code in Gradle functional tests"
  }
}

processTestResources {
  def tokens = [COMPILE_SDK_VERSION: COMPILE_SDK_VERSION,
                BUILD_TOOLS_VERSION: BUILD_TOOLS_VERSION,
                MIN_SDK_VERSION    : SAMPLE_MIN_SDK_VERSION,
                TARGET_SDK_VERSION : TARGET_SDK_VERSION]

  inputs.properties(tokens)

  from(sourceSets.test.resources.srcDirs) {
    include '**/testenv.properties'
    filter(ReplaceTokens, tokens: tokens)
  }
}

test {
  failFast = true
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat = "full"
  }

  // Enable this line to run disable running Functional Tests on the local device
//  environment("CI", "true")
}

dependencies {
  // Test Plugin Dependencies
  testImplementation project(":android-junit5")

  // Utilities for assertions & tests in general
  testImplementation "commons-io:commons-io:$APACHE_COMMONS_VERSION"
  testImplementation "commons-lang:commons-lang:$APACHE_COMMONS_VERSION"

  // Dependent Plugins
  testImplementation "com.android.tools.build:gradle:$ANDROID_PLUGIN_VERSION"
  testImplementation "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"

  // Test Execution
  testImplementation "junit:junit:$JUNIT4_VERSION"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$JUNIT_JUPITER_VERSION"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$JUNIT_JUPITER_VERSION"
  testImplementation "org.junit.jupiter:junit-jupiter-engine:$JUNIT_JUPITER_VERSION"
  testImplementation "org.jetbrains.spek:spek-api:$SPEK_VERSION"
  testRuntimeOnly "org.jetbrains.spek:spek-junit-platform-engine:$SPEK_VERSION"
  testImplementation "org.junit-pioneer:junit-pioneer:$JUNIT_PIONEER_VERSION"

  testImplementation "org.assertj:assertj-core:$ASSERTJ_VERSION"
  testImplementation "org.mockito:mockito-core:$MOCKITO_VERSION"

  // Compilation of local classpath for functional tests
  functionalTest "junit:junit:$JUNIT4_VERSION"
  functionalTest "com.android.tools.build:gradle:$ANDROID_PLUGIN_VERSION"
  functionalTest "org.junit.jupiter:junit-jupiter-api:$JUNIT_JUPITER_VERSION"
  functionalTest "org.junit.jupiter:junit-jupiter-engine:$JUNIT_JUPITER_VERSION"
  functionalTest "org.junit.platform:junit-platform-console:$JUNIT_PLATFORM_VERSION"
  functionalTest "org.jetbrains.kotlin:kotlin-compiler-embeddable:$KOTLIN_VERSION"
}

// Resource Writers
task writePluginClasspath(type: WriteClasspathResource) {
  inputFiles = sourceSets.test.runtimeClasspath
  outputDir = file("$buildDir/resources/test")
  resourceFileName = "plugin-classpath.txt"
}

task writeFunctionalTestCompileClasspath(type: WriteClasspathResource) {
  inputFiles = configurations.functionalTest
  outputDir = file("$buildDir/resources/test")
  resourceFileName = "functional-test-compile-classpath.txt"
}

tasks.withType(WriteClasspathResource).all {
  processTestResources.finalizedBy it
  test.mustRunAfter it
}
